<?xml version="1.0" encoding="UTF-8"?>
<!DOCTYPE script:module PUBLIC "-//OpenOffice.org//DTD OfficeDocument 1.0//EN" "module.dtd">
<script:module xmlns:script="http://openoffice.org/2000/script" script:name="ExpandAnimations" script:language="StarBasic">
&#39; ExpandAnimations (https://github.com/monperrus/ExpandAnimations)

&#39; Copyright 2009-2011 Matthew Neeley.
&#39; Copyright 2011 Martin Monperrus.

&#39; This program is free software: you can redistribute it and/or modify
&#39; it under the terms of the GNU Lesser General Public License as published by
&#39; the Free Software Foundation, either version 3 of the License, or
&#39; (at your option) any later version.

&#39; This program is distributed in the hope that it will be useful,
&#39; but WITHOUT ANY WARRANTY; without even the implied warranty of
&#39; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
&#39; GNU Lesser General Public License for more details.

&#39; You should have received a copy of the GNU Lesser General Public License
&#39; along with this program.  If not, see &lt;http://www.gnu.org/licenses/&gt;.

Private ANIMSET as String
Private ENUMACCESS as String
Private VISATTR as String


&#39; expands the current document and saves it to PDF
&#39; the expanded version is saved to disk in filename-expanded.odp
&#39; the PDF is filename-expanded.pdf
sub Main 
  Dim doc As Object
  doc = thisComponent
  
  &#39; the expansion 
  newUrlPdf = expandAnimations(doc)
  
  msgbox &quot;Expansion done! See &quot;+newUrlPdf
end sub

&#39; tests the module on /tmp/test.odp
&#39; can be called on the command line with
&#39; $ libreoffice &quot;macro:///ExpandAnimations.ExpandAnimations.test&quot;
sub test
  Dim Dummy()
  Url = &quot;file:///home/martin/test-ExpandAnimations.odp&quot;
  StarDesktop.loadComponentFromURL(Url, &quot;_default&quot;, 0, Dummy)
  Main
end sub

&#39; expands the animations and exports to PDF
function expandAnimations(doc as Object)

  &#39; rename the document
  docExpanded = renameAsExpanded(doc)
  
  &#39; expand it
  expandDocument(docExpanded)
  
  &#39; export to PDF
  &#39; returns the PDF file name
  expandAnimations= exportToPDF(docExpanded)  
  
  &#39; closing the expanded version  
  docExpanded.close(false)
  
end function


&#39; renames the current document
&#39; e.g. test.odp -&gt; test-expanded.odp
function renameAsExpanded(doc as Object)
  Dim Dummy()
  
  If Not BasicLibraries.isLibraryLoaded(&quot;Tools&quot;) then
     BasicLibraries.loadLibrary(&quot;Tools&quot;)
  Endif

  sDocUrl = doc.getURL()
  sDocPath = DirectoryNameoutofPath(sDocUrl, &quot;/&quot;)
  sDocFileName = FileNameoutofPath(sDocUrl, &quot;/&quot;)
  sDocFileNameExtension = GetFileNameExtension(sDocUrl)
  sDocFileNameWithoutExtension = GetFileNameWithoutExtension(sDocUrl, &quot;/&quot;) 
  
  newUrlExpanded = sDocPath + &quot;/&quot; + sDocFileNameWithoutExtension + &quot;-expanded.odp&quot;
  doc.storeToUrl(newUrlExpanded, Array())
  
  &#39; reloading the old document
  expandedDoc = StarDesktop.loadComponentFromURL(newUrlExpanded, &quot;_default&quot;, 0, Dummy)  
  
  renameAsExpanded = expandedDoc
end function


&#39; exports to PDF
function exportToPDF(doc as Object)
  If Not BasicLibraries.isLibraryLoaded(&quot;Tools&quot;) then
     BasicLibraries.loadLibrary(&quot;Tools&quot;)
  Endif

  sDocUrl = doc.getURL()
  sDocPath = DirectoryNameoutofPath(sDocUrl, &quot;/&quot;)
  sDocFileName = FileNameoutofPath(sDocUrl, &quot;/&quot;)
  sDocFileNameExtension = GetFileNameExtension(sDocUrl)
  sDocFileNameWithoutExtension = GetFileNameWithoutExtension(sDocUrl, &quot;/&quot;) 

  newUrlPdf = sDocPath + &quot;/&quot; + sDocFileNameWithoutExtension + &quot;.pdf&quot;
  &#39; we use storeToUrl because we don&#39;t want to load the PDF
  &#39; actually we have to, otherwise, there is an error
  doc.storeToUrl(newUrlPdf, Array(makePropertyValue(&quot;FilterName&quot;, &quot;impress_pdf_Export&quot;)))
  exportToPDF = newUrlPdf
end function 



&#39; creates and returns a new com.sun.star.beans.PropertyValue.
&#39; see http://www.oooforum.org/forum/viewtopic.phtml?t=5108
Function makePropertyValue( Optional cName As String, Optional uValue ) As com.sun.star.beans.PropertyValue
   Dim oPropertyValue As New com.sun.star.beans.PropertyValue
   If Not IsMissing( cName ) Then
      oPropertyValue.Name = cName
   EndIf
   If Not IsMissing( uValue ) Then
      oPropertyValue.Value = uValue
   EndIf
   makePropertyValue() = oPropertyValue
End Function 


function expandDocument(doc as Object)
    ANIMSET = &quot;com.sun.star.animations.XAnimateSet&quot;
    ENUMACCESS = &quot;com.sun.star.container.XEnumerationAccess&quot;
    VISATTR = &quot;Visibility&quot;
    
    numSlides = doc.getDrawPages().getCount()
        
    &#39; go through pages in reverse order
    for i = numSlides-1 to 0 step -1
        slide = doc.drawPages(i)
        agordiGrandoDeTekstoj(slide)
        if hasAnimation(slide) then
            n = countAnimationSteps(slide)
            if n &gt; 1 then
                origName = slide.Name
                replicateSlide(doc, slide, n)
                visArray = getShapeVisibility(slide, n)
                for frame = 0 to n-1
                    currentSlide = doc.drawPages(i + frame)
                    currentSlide.Name = origName &amp; &quot; (&quot; &amp; CStr(frame+1) &amp; &quot;)&quot;
                    removeInvisibleShapes(currentSlide, visArray, frame)
                next
            end if
        end if
    next
    finalSlides = doc.getDrawPages().getCount()
    &#39;MsgBox(&quot;Done! Expanded &quot; &amp; CStr(numSlides) &amp; &quot; slides to &quot; &amp; CStr(finalSlides) &amp; &quot;.&quot;)
    
    &#39; saving the expanded version
  doc.store()

end function


&#39; Agordi grando de tekstoj (The English language discriminate by country of birth)
function agordiGrandoDeTekstoj(slide as Object)
    Dim numObjektoj As Integer
    Dim i As Integer
    Dim objekto As Object
    Dim eNum As Object
    Dim oTipo As String
    Dim oTeksto As Object
    Dim oAlghustigo As Integer
    Dim io(5) As Integer
    Dim kk As Integer
    
    numObjektoj = slide.getCount()
    for i = 0 to numObjektoj-1
        objekto = slide.getByIndex(i)
        oTipo = objekto.GetShapeType()
        if oTipo = &quot;com.sun.star.drawing.CustomShape&quot; then
            objekto.TextAutoGrowWidth = false
        end if
        if oTipo = &quot;com.sun.star.drawing.TextShape&quot; then
            oAlghustigo = -1
            for kk = 0 to 4
                io(kk) = 0
            next
            eNum = objekto.Text.CreateEnumeration
            while eNum.HasMoreElements
                oTeksto = eNum.NextElement
                &#39; Chu la ero estas alineo?
                if oTeksto.SupportsService(&quot;com.sun.star.text.Paragraph&quot;) then
                    if oTeksto.ParaAdjust &lt; 4 then
                        io(oTeksto.ParaAdjust) = 1
                    end if
                    select case oTeksto.ParaAdjust
                        case com.sun.star.style.ParagraphAdjust.LEFT
                        if oAlghustigo = -1 then
                            oAlghustigo = com.sun.star.drawing.TextHorizontalAdjust.LEFT
                        end if
                    case com.sun.star.style.ParagraphAdjust.RIGHT
                        oAlghustigo = com.sun.star.drawing.TextHorizontalAdjust.RIGHT
                    case com.sun.star.style.ParagraphAdjust.BLOCK, com.sun.star.style.ParagraphAdjust.STRETCH
                        if oAlghustigo &lt; 1 then
                            oAlghustigo = com.sun.star.drawing.TextHorizontalAdjust.BLOCK
                        end if
                    case com.sun.star.style.ParagraphAdjust.CENTER
                        if oAlghustigo &lt; 1 then
                            oAlghustigo = com.sun.star.drawing.TextHorizontalAdjust.CENTER
                        end if
                    end select
                end if
            wend
            objekto.TextAutoGrowWidth = false
            &#39; TextHorizontalAdjust: LEFT 0, CENTER 1, RIGHT 2, BLOCK 3
            &#39; ParaAdjust: LEFT 0, RIGHT 1, BLOCK 2, CENTER 3, STRETCH 4
            if io(0)+io(1)+io(2)+io(3)+io(4) = 1 then
                objekto.TextHorizontalAdjust = oAlghustigo
            end if
            if io(0)+io(1)+io(2)+io(3)+io(4) &gt; 1 then
                objekto.TextHorizontalAdjust = com.sun.star.drawing.TextHorizontalAdjust.BLOCK
            end if
        end if
    next
end function


&#39; get a list of all shapes whose visibility is changed during the animation
function getAnimatedShapes(slide as Object)
     shapes = Array() &#39; start with an empty array
 
     if not hasAnimation(slide) then
         getAnimatedShapes = shapes
         exit function
     end if
     
    mainSequence = getMainSequence(slide)    
    clickNodes = mainSequence.createEnumeration()
    while clickNodes.hasMoreElements()
        clickNode = clickNodes.nextElement()

        groupNodes = clickNode.createEnumeration()
        while groupNodes.hasMoreElements()
            groupNode = groupNodes.nextElement()

            effectNodes = groupNode.createEnumeration()
            while effectNodes.hasMoreElements()
                effectNode = effectNodes.nextElement()

                animNodes = effectNode.createEnumeration()
                while animNodes.hasMoreElements()
                    animNode = animNodes.nextElement()
                    if isVisibilityAnimation(animNode) then
                        target = animNode.target
                        if not IsEmpty(target) then 
                          &#39; if we haven&#39;t seen this shape yet, add it to the array
                          if not containsObject(shapes, target) then
                            newUBound = UBound(shapes) + 1
                            reDim preserve shapes(newUBound)
                            shapes(newUBound) = target
                          end if
                        end if
                     end if
                 wend
            wend
        wend
    wend
    getAnimatedShapes = shapes
end function


&#39; create a 2-D array giving the visibility of each animated
&#39; shape for each frame in the expanded animation
function getShapeVisibility(slide as Object, nFrames as Integer)
     shapes = getAnimatedShapes(slide)
     dim visibility(UBound(shapes), nFrames-1) as Boolean
     
     &#39; loop over all animated shapes
     for n = 0 to UBound(shapes)
         shape = shapes(n)
         visKnown = false
         visCurrent = false
     
         &#39; iterate over the animations for this slide,
         &#39; looking for those that change the visibility
         &#39; of the current shape
         mainSequence = getMainSequence(slide)
        if HasUnoInterfaces(mainSequence, ENUMACCESS) then            
            clickNodes = mainSequence.createEnumeration()
            currentFrame = 0
            while clickNodes.hasMoreElements()
                clickNode = clickNodes.nextElement()
     
                groupNodes = clickNode.createEnumeration()
                while groupNodes.hasMoreElements()
                    groupNode = groupNodes.nextElement()
     
                    effectNodes = groupNode.createEnumeration()
                    while effectNodes.hasMoreElements()
                        effectNode = effectNodes.nextElement()
     
                         animNodes = effectNode.createEnumeration()
                         while animNodes.hasMoreElements()
                             animNode = animNodes.nextElement()
                             if isVisibilityAnimation(animNode) then
                                target = animNode.target
                                &#39; if this is the shape we want, check the visibility
                                sameStruct = false
                                if IsUnoStruct(target) AND IsUnoStruct(shape) then
	                                sameStruct = EqualUnoObjects(shape.Shape, target.Shape) AND shape.Paragraph=target.Paragraph
                                end if
                                if EqualUnoObjects(shape, target) OR sameStruct then
                                    visCurrent = animNode.To
                                    &#39; if this is the first time we&#39;ve seen this
                                    &#39; shape, set the visibility on the previous frames
                                    if visKnown = false then
                                        for i = 0 to currentFrame
                                            visibility(n, i) = not visCurrent
                                        next
                                        visKnown = true
                                    end if
                                end if
                             end if
                         wend
                     wend
                wend
                currentFrame = currentFrame + 1
                visibility(n, currentFrame) = visCurrent
            wend
        end if
    next
    getShapeVisibility = visibility
end function


&#39; remove from the given slide all shapes that are invisible in the specified frame
sub removeInvisibleShapes(slide as Object, visibility, frame as Integer)
     shapes = getAnimatedShapes(slide)
     for n = 0 to UBound(shapes)
        if visibility(n, frame) = false then
            &#39;special handling for com.sun.star.presentation.ParagraphTarget
            if (IsUnoStruct(shapes(n))) then
                shape=shapes(n).Shape
                para = shapes(n).Paragraph
                count = 0
                eNum =  shape.createEnumeration
                &#39; for each paragraph in textbox
                Do while eNum.HasMoreElements()
                    oAObj = eNum.nextElement()
                    &#39; remove those whose index larger than shapes(n).Paragraph
                    if count = para then
                        oAObj.String = &quot;&quot;
                        oAObj.NumberingIsNumber = false
                    end if
                    count = count + 1
                Loop
            else
                slide.remove(shapes(n))
            end if
        end if
    next
end sub


&#39; checks if the given animation node changes a shape&#39;s visibility
function isVisibilityAnimation(animNode as Object) as Boolean
    isVisibilityAnimation = HasUnoInterfaces(animNode, ANIMSET) and _
                            (animNode.AttributeName = VISATTR)
end function


&#39; check if an object (needle) is contained in an array (haystack)
function containsObject(haystack as Object, needle as Object) as Boolean
    containsObject = false
    for each item in haystack
        if EqualUnoObjects(item, needle) then
            containsObject = true
            exit function
        end if
    next item
end function


&#39; determine whether the given drawPage has animations attached
function hasAnimation(slide as Object) as Boolean
    mainSequence = getMainSequence(slide)
    hasAnimation = HasUnoInterfaces(mainSequence, ENUMACCESS)
end function


&#39; count the number of frames in the animation for the given slide
function countAnimationSteps(slide as Object) as Integer
    mainSequence = getMainSequence(slide)
    countAnimationSteps = countElements(mainSequence) + 1
end function


&#39; count the number of elements in an enumerable object
function countElements(enumerable as Object) as Integer
    oEnum = enumerable.createEnumeration()
    n = 0
    while oEnum.hasMoreElements()
        n = n + 1
        oEnum.nextElement()
    wend
    countElements = n
end function


&#39; make n-1 copies of the given slide in the given doc
&#39; when done, the slide will appear a total of n times
&#39; note that doc.duplicate adds copies immediately after
&#39; the page being copied
function replicateSlide(doc, slide, n)
    for i = 1 to n-1
        doc.duplicate(slide)
    next
end function


&#39; get the main sequence from the given draw page
function getMainSequence(oPage as Object) as Object
    on error resume next
    mainSeq = com.sun.star.presentation.EffectNodeType.MAIN_SEQUENCE

    oNodes = oPage.AnimationNode.createEnumeration()
    while oNodes.hasMoreElements()
        oNode = oNodes.nextElement()
        if getNodeType(oNode) = mainSeq then
            getMainSequence = oNode
            exit function
        end if
    wend
end function


&#39; get the type of a node
function getNodeType(oNode as Object) as Integer
    on error resume next
    for each oData in oNode.UserData
        if oData.Name = &quot;node-type&quot; then
            getNodeType = oData.Value
            exit function
        end if
    next oData
end function


&#39; get the class of an effect
function getEffectClass(oEffectNode as Object) as String
    on error resume next
    for each oData in oEffectNode.UserData
        if oData.Name = &quot;preset-class&quot; then
            getEffectClass = oData.Value
            exit function
        end if
    next oData
End Function


&#39; get the id of an effect
function getEffectId(oEffectNode as Object) as String
    on error resume next
    for each oData in oEffectNode.UserData
        if oData.Name = &quot;preset-id&quot; then
            getEffectId = oData.Value
            exit function
        end if
    next oData
end function
</script:module>
